# 1 mio hands
generate 1000000

hw = hcp(west)
spw = spades(west)
hew = hearts(west)
diw = diamonds(west)
clw = clubs(west)
he = hcp(east)

opening_values = ( hw >= 12 and hw <= 19 )

balanced = shape(west, any 4333 + any 5332 + any 4432)

open_1nt = ( hw >= 15 and hw <= 17 and
	 balanced )

open_one_suit = opening_values and not open_1nt

open_one_spade = ( open_one_suit and
  shape{west, 5+xxx} and
  spw >= hew and spw >= diw and spw >= clw
)

open_one_heart = ( open_one_suit and
  shape{west, x5+xx} and
  hew > spw and hew >= diw and hew >= clw
)

open_one_diamond = ( open_one_suit and
  shape{west, 4-4-4+x:d>=c + 4432 + 5+M6+d(xx):d>s,d>h}
)

open_one_club = ( open_one_suit and
  shape{west, 4-4-xx:c>d + 4333 + 3433 + 5+M6+c(xx):c>s,c>h}
)

weak_two_values = ( hw >= 5 and hw <= 11 )
no_four_major = ( spades(west) < 4 and hearts(west) < 4 )

weak_two_diamonds = (
	weak_two_values and shape(west, xx6x) and
	no_four_major and hcp( west, diamonds ) >= 3 )

weak_two_hearts =  (
	weak_two_values and shape(west, x6xx) and
	spades(west) < 4 and hcp( west, hearts ) >= 3 )

weak_two_spades =  (
	weak_two_values and shape(west, 6xxx) and
	hearts(west) < 4 and hcp( west, spades ) >= 3 )

single_suiter =	(
	hearts(west) >= 6 or spades(west) >= 6 or
	diamonds(west) >= 6 or clubs(west) >= 6 )

semiforcing = (
	loser(west) == 5 and
	hw >= 16 and
	single_suiter )

gameforcing = (
	loser(west) < 5 and
	hw >= 18 and
	single_suiter )

semi_nt = (
	balanced and
	( hw >= 22 and hw <= 23 or
	  hw >= 26 and hw <= 27 ) )

gf_nt = (
	balanced and
	( hw >= 24 and hw <= 25 or
	  hw >= 28 ) )

# semi-forcing or weak two diamonds
open_two_clubs = (
	weak_two_diamonds or
	semiforcing or
	semi_nt )

# game-forcing or weak two hearts
open_two_diamonds = (
	weak_two_hearts or
	gameforcing or
	gf_nt )	

# two-suiter incl. hearts
open_two_hearts = (
	hearts(west) >= 5 and
	( clubs(west) >= 5 or
	  diamonds(west) >= 5 or
	  spades(west) >= 5 ) and
	hw >= 5 and hw <= 10 )

# weak two spades
open_two_spades = ( weak_two_spades )

# 2NT
open_two_notrump = (
	balanced and
	hw >= 20 and hw <= 21 )

# gambling
gambling = ( top2(west, spades) == 0 and top2(west, hearts) == 0 and
clubs(west) < 9 and diamonds(west) < 9 and
( ( clubs(west) >= 6 and top4(west, clubs) == 4 or
    clubs(west) >= 7 and top3(west, clubs) == 3 and hascard(west, TC) )
  and top2(west, diamonds) == 0
or
  ( diamonds(west) >= 6 and top4(west, diamonds) == 4 or
    diamonds(west) >= 7 and top3(west, diamonds) == 3 and hascard(west, TD) )
  and top2(west, clubs) == 0
) )


# 3 in minor
open_three_minor = (
	hw >= 5 and hw <= 10 and
	( diamonds(west) >= 7 or clubs(west) >= 7 )
	and no_four_major and not gambling
)

# 3 in major
open_three_major = (
	hw >= 5 and hw <= 10 and
	( hearts(west) >= 7 or spades(west) >= 7 ) )

# walsh slam relays
walsh_slam = (
open_one_club and balanced and hw >= 12 and hw <= 14 and
(clubs(east) < 5 or diamonds(east) >= clubs(east)) and
diamonds(east) > spades(east) and diamonds(east) > hearts(east) and
hearts(east) < 4 and spades(east) < 4 and diamonds(east) >= 3 and he >= 14
)

open_one_minor = open_one_club + open_one_diamond
open_one_major = open_one_heart + open_one_spade

action	average "1SA" open_1nt,
#	average "1x" open_one_suit,
	average "1C" open_one_club,
	average "1D" open_one_diamond,
	average "1m" open_one_minor,
	average "1H" open_one_heart,
	average "1S" open_one_spade,
	average "1M" open_one_major,
	average "w2D" weak_two_diamonds,
	average "w2H" weak_two_hearts,
	average "2C" open_two_clubs,
	average "2D" open_two_diamonds,
	average "2H" open_two_hearts,
	average "2S" open_two_spades,
	average "2nt" open_two_notrump,
	average "3m" open_three_minor,
	average "3M" open_three_major,
	average "G" gambling,
	average "ws" walsh_slam
